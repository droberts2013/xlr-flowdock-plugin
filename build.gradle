plugins {
  id "com.github.hierynomus.license" version "0.14.0"
  id "com.xebialabs.xl.docker" version "1.1.0"
  id 'nebula.release' version '6.0.0'
  id "com.github.hierynomus.jython" version "0.8.0"
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'maven'

version = 'ge-1.2.2'

xlDocker {
  compileImage = 'xebialabs/xlr_dev_compile'
  compileVersion = 'v7.2.0.2'
  runImage = 'xebialabs/xlr_dev_run'
  runVersion = 'v7.2.0.1'
  runPortMapping = '15516:5516'
}

def apiVersion = '2017.6.0' 

if (!project.hasProperty('release.scope')) {
  project.ext['release.scope'] = 'patch'
}

if (!project.hasProperty('release.useLastTag')) {
  project.ext['release.useLastTag'] = true
}

def xlreleaseHome = "../xlr-lib/xl-release-7.5.0-server"

repositories {
  mavenLocal()
  mavenCentral()
  maven {
    url 'https://dist.xebialabs.com/public/maven2'
  }
}

dependencies {
  compile "com.xebialabs.deployit:udm-plugin-api:$apiVersion"
  compile "com.xebialabs.deployit:engine-spi:$apiVersion"
  compile "com.xebialabs.deployit:engine-api:$apiVersion"
  compile group: "com.xebialabs.xl-platform", name: "xl-repository-api", version: "$apiVersion", transitive: false
  compile "nl.javadude.t2bus:t2-bus:1.2.1"
  jython ":requests:2.11.1"
  jython(":python-flowdock:0.1") {
    artifact {
      name = "flowdock"
      extension = "py"
    }
  }
  compile fileTree(dir: "$xlreleaseHome/hotfix", include: ['*.jar'])
  compile fileTree(dir: "$xlreleaseHome/lib", include: ['*.jar'])
  compile group: 'com.googlecode.json-simple', name: 'json-simple', version: '1.1.1'
  compile group: 'org.apache.jackrabbit', name: 'jackrabbit-jcr-commons', version: '2.16.0'
  
}

license {
  header rootProject.file('License.md')
  strictCheck false
  ext.year = Calendar.getInstance().get(Calendar.YEAR)
  ext.name = 'XEBIALABS'
  excludes(["**/requests/**/*.py", "**/flowdock.py"])
}
